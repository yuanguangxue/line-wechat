/*
 * Copyright 2018 LINE Corporation
 *
 * LINE Corporation licenses this file to you under the Apache License,
 * version 2.0 (the "License"); you may not use this file except in compliance
 * with the License. You may obtain a copy of the License at:
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
 * License for the specific language governing permissions and limitations
 * under the License.
 */

apply plugin: 'org.springframework.boot'

dependencies {
    compile project(':line-bot-spring-boot')
    compile 'com.google.guava:guava'
}

bootRepackage {
    enabled = true
    executable = true
    // Custom Launch Script avoid https://github.com/spring-projects/spring-boot/issues/5164
    embeddedLaunchScript = file('src/main/resources/launch.script')
    withJarTask = jar
    classifier = 'exec'
    mainClass = 'com.linecorp.bot.cli.Application'
}

// Reset bootRepackage dependency to remove dependency from bootRepackage to signArchives.
// This causes circular dependency.
//
// Memo: By default, bootRepackage dependsOn the all tasks creating jar including signArchives task.
// But signArchives should be after repackages.
bootRepackage.dependsOn.clear()
bootRepackage.dependsOn jar, findMainClass
signArchives.dependsOn bootRepackage

// Add exec jar into archives to be uploaded.
artifacts.add('archives', file("$buildDir/libs/${project.name}-${project.version}-${bootRepackage.classifier}.jar")) {
    classifier = bootRepackage.classifier
}
